* DONE Exporting numbered citations in html with unsorted numbered bibliography
  CLOSED: [2014-05-17 Sat 14:42]
  :PROPERTIES:
  :categories: org-mode
  :END:

In this [[http://kitchingroup.cheme.cmu.edu/blog/2014/05/17/Exporting-citations-in-html/][post]] we illustrated a simple export of org-ref citations to html. This was a simple export that simply replaced each citation with a hyperlink. Today we look at formatting them with numbers, and having a numbered bibliography. This will take multiple passes to achieve. One pass to get the citations and calculate replacements, and one pass to replace them.

This text is just some text with somewhat random citations in it for seeing it work. You might like my two data sharing articles cite:kitchin-2015-examp,kitchin-2015-data-surfac-scien. We illustrate the use of org-mode in publishing computational work cite:xu-2015-tunin-oxide,mehta-2014-ident-poten,curnan-2014-effec-concen, experimental cite:hallenbeck-2013-effec-o2 and mixed computational and experimental work cite:miller-2014-simul-temper,boes-2015-estim-bulk. This example will correctly number multiple references to a citation, e.g.  cite:kitchin-2015-examp and cite:kitchin-2015-data-surfac-scien.

This post is somewhat long, and the way I worked it out is at the end ([[id:1D63E1FB-55CD-48B7-B5E1-D0AC5E4D989B][Long appendix illustrating how we got the code to work]]). The short version is that we do some preprocessing to get the citations in the document, calculate replacement values for them and the bibliography, replace them in the org-buffer /before/ the export in the backend (html) format we want, and then conclude with the export. This is proof of concept work.

The main issues you can see are:
1. Our formatting code is very rudimentary, and relies on reftex. It is not as good as bibtex, or presumably some citation processor. Major improvements would require abandoning the reftex approach to use something that builds up the bibliography entry, allows modification of author names, and accomodates missing information gracefully.
2. The bibliography contents reflect the contents of my bibtex file, which is LaTeX compatible. We could clean it up more, by either post-processing to remove some things like escaped &, or by breaking compatibility with LaTeX.
3. The intext citations could use some fine tuning on spaces, e.g. to remove trailing spaces after words, or to move superscripts to the right of punctuation, or to adjust spaces after some citations.
4. Changing the bibliography style for each entry amounts to changing a variable for the bibliography. We have to modify a function to change the intext citation style, e.g. to brackets, or (author year).
5. I stuck with only cite links here. It would not get a citenum format correct, e.g. it should not be superscripted in this case, or a citeauthor format correct. That would require some code in the replacement section that knows how to replace different types of citations.

The  org-ref-unsrt-html-processor function could be broken up more, and could take some parameters to fine-tune some of these things.

bibliographystyle:unsrt

[[bibliography:~/Dropbox/bibliography/references.bib]]

** The working code
Here is a funtion to process the org file prior parsing during the export process. This function goes into org-export-before-parsing-hook, and takes one argument, the backend. We simply replace all the citation links with formatted HTML snippets or blocks. If the snippets get longer than a line, it will break.

We use org-ref-reftex-format-citation to generate the bibliography, which uses reftex to format a string with escape characters in it.

#+BEGIN_SRC emacs-lisp
(setq org-ref-bibliography-entry-format
      '(("article" . "<li><a id=\"\%k\">%a, %t, <i>%j</i>, <b>%v(%n)</b>, %p (%y)</a>. <a href=\"%U\">link</a>. <a href=\"http://dx.doi.org/%D\">doi</a>.</li>")
	("book" . "<li><a id=\"\%k\">%a, %t, %u (%y).</a></li>")))

(defun org-ref-unsrt-html-processor ()
  "Citation processor function for the unsrt style with html output."
  (let (links
	unique-keys numbered-keys
	replacements
	bibliography-link
	bibliographystyle-link
        bibliography)
    ;; step 1 - get the citation links
    (setq links (loop for link in (org-element-map
				      (org-element-parse-buffer) 'link 'identity)
		      if (-contains?
			  org-ref-cite-types
			  (org-element-property :type link))
		      collect link))

    ;; list of unique keys. '((key number))
    (setq unique-keys (loop for i from 1
			    for key in (org-ref-get-bibtex-keys)
			    collect (list key (number-to-string i))))


    ;; (start end replacement-text)
    (setq replacements
	  (loop for link in links
		collect
		(let ((path (org-element-property :path link)))
		  (loop for (key number) in unique-keys
			do
			(setq
			 path
			 (replace-regexp-in-string
			  key (format "<a href=\"#%s\">%s</a>" key number)
			  path)))
		  (list (org-element-property :begin link)
			(org-element-property :end link)
			(format "@@html:<sup>%s</sup>@@" path)))))


    (setq bibliography
	  (concat "#+begin_html
<h1>Bibliography</h1><ol>"
		  (mapconcat
		   'identity
		   (loop for (key number) in unique-keys
			 collect
			 (let* ((result (org-ref-get-bibtex-key-and-file key))
				(bibfile (cdr result))
				(entry (save-excursion
					 (with-temp-buffer
					   (insert-file-contents bibfile)
					   (bibtex-set-dialect
					    (parsebib-find-bibtex-dialect) t)
					   (bibtex-search-entry key)
					   (bibtex-parse-entry)))))
			   (org-ref-reftex-format-citation
			    entry
			    (cdr (assoc (cdr (assoc "=type=" entry))
					org-ref-bibliography-entry-format)))))
		   "")
		  "</ol>
,#+end_html"))

    ;; now, we need to replace each citation. We do that in reverse order so the
    ;; positions do not change.
    (loop for (start end replacement) in (reverse replacements)
	  do
	  (setf (buffer-substring start end) replacement))

    ;; Eliminate bibliography style links
    (loop for link in (org-element-map
			  (org-element-parse-buffer) 'link 'identity)
	  if (string= "bibliographystyle"
		      (org-element-property :type link))
	  do
	  (setf (buffer-substring (org-element-property :begin link)
				  (org-element-property :end link))
		""))

    ;; replace the bibliography link
    (setq bibliography-link (loop for link in (org-element-map
						  (org-element-parse-buffer) 'link 'identity)
				  if (string= "bibliography"
					      (org-element-property :type link))
				  collect link))
    (if (> (length bibliography-link) 1)
	(error "Only one bibliography link allowed"))

    (setq bibliography-link (car bibliography-link))
    (setf (buffer-substring (org-element-property :begin bibliography-link)
			    (org-element-property :end bibliography-link))
	  bibliography)))


(defun org-ref-citation-processor (backend)
  "Figure out what to call and call it"
  (let (bibliographystyle)
    (setq
     bibliographystyle
     (org-element-property
      :path (car
	     (loop for link in
		   (org-element-map
		       (org-element-parse-buffer) 'link 'identity)
		   if (string= "bibliographystyle"
			       (org-element-property :type link))
		   collect link))))
    (funcall (intern (format "org-ref-%s-%s-processor" bibliographystyle backend)))))

(add-hook 'org-export-before-parsing-hook 'org-ref-citation-processor)

(browse-url (org-html-export-to-html))
#+END_SRC

#+RESULTS:
: #<process open ./blog.html>


** Long appendix illustrating how we got the code to work
   :PROPERTIES:
   :ID:       1D63E1FB-55CD-48B7-B5E1-D0AC5E4D989B
   :END:
The first thing we need is a list of all the citation links, in the order cited. Here they are.

#+BEGIN_SRC emacs-lisp
(mapcar
 (lambda (link) (org-element-property :path link))
 (loop for link in (org-element-map (org-element-parse-buffer) 'link 'identity)
       if (-contains? org-ref-cite-types (org-element-property :type link))
       collect link))
#+END_SRC
#+RESULTS:
| armiento-2014-high | daza-2014-carbon-dioxid,mehta-2014-ident-poten,suntivich-2014-estim-hybrid | armiento-2014-high | alesi-2012-evaluat-primar | day-1995-scien-englis | armiento-2014-high |

Now, we need to compute replacements for each citation link, and construct the bibliography. We will make a numbered, unsorted bibliography, and we want to replace each citation with the corresponding numbers, hyperlinked to the entry.

We start with a list of the keys in the order cited, and a number we will use for each one.

#+BEGIN_SRC emacs-lisp
(loop for i from 1
      for key in (org-ref-get-bibtex-keys)
      collect (list key i))
#+END_SRC

#+RESULTS:
| armiento-2014-high          | 1 |
| daza-2014-carbon-dioxid     | 2 |
| mehta-2014-ident-poten      | 3 |
| suntivich-2014-estim-hybrid | 4 |
| alesi-2012-evaluat-primar   | 5 |
| day-1995-scien-englis       | 6 |

Now, we need to compute replacements for each cite link. This will be replacing each key with the number above. We will return a list of ((start end) . "replacement text") that we can use to replace each link. For fun, we make these superscripted html.

#+BEGIN_SRC emacs-lisp
(let ((links (loop for link in (org-element-map (org-element-parse-buffer) 'link 'identity)
		   if (-contains? org-ref-cite-types (org-element-property :type link))
		   collect link))
      (replacements (loop for i from 1
			  for key in (org-ref-get-bibtex-keys)
			  collect (list key (number-to-string i)))))
  (loop for link in links
	collect (let ((path (org-element-property :path link)))
		  (dolist (repl replacements)
		    (setq path (replace-regexp-in-string (car repl) (nth 1 repl) path)))
		  (list (org-element-property :begin link)
			(org-element-property :end link)
			(format "<sup>%s</sup>" path)))))
#+END_SRC

#+RESULTS:
|  604 |  628 | <sup>1</sup>     |
|  643 |  723 | <sup>2,3,4</sup> |
|  803 |  826 | <sup>1</sup>     |
|  924 |  954 | <sup>5</sup>     |
|  996 | 1022 | <sup>6</sup>     |
| 1062 | 1085 | <sup>1</sup>     |

We also need to compute the bibliography for each key. We will use org-ref-reftex-format-citation to do this. For that we need the parsed bibtex entries, and a format string. org-ref provides most of this.

#+BEGIN_SRC emacs-lisp :results html
(setq org-ref-bibliography-entry-format
      '(("article" . "<li>%a, %t, <i>%j</i>, <b>%v(%n)</b>, %p (%y). <a href=\"%U\">link</a>. <a href=\"http://dx.doi.org/%D\">doi</a>.</li>")
	("book" . "<li>%a, %t, %u (%y).</li>")))

(concat "<h1>Bibliography</h1><br><ol>"
	(mapconcat
	 'identity
	 (loop for key in (org-ref-get-bibtex-keys)
	       collect
	       (let* ((result (org-ref-get-bibtex-key-and-file key))
		      (bibfile (cdr result))
		      (entry (save-excursion
			       (with-temp-buffer
				 (insert-file-contents bibfile)
				 (bibtex-set-dialect (parsebib-find-bibtex-dialect) t)
				 (bibtex-search-entry key)
				 (bibtex-parse-entry)))))
		 (org-ref-reftex-format-citation
		  entry
		  (cdr (assoc (cdr (assoc "=type=" entry))
			      org-ref-bibliography-entry-format)))))
	 "")
	"</ol>")
#+END_SRC

#+RESULTS:
#+BEGIN_HTML
<h1>Bibliography</h1><br><ol><li>Armiento, Kozinsky, Hautier, Fornari, \& Ceder, High-Throughput Screening of Perovskite Alloys for  Piezoelectric Performance and Thermodynamic Stability, <i>{Phys. Rev. B}</i>, <b>89()</b>, 134103 (2014). <a href="http://link.aps.org/doi/10.1103/PhysRevB.89.134103">link</a>. <a href="http://dx.doi.org/10.1103/PhysRevB.89.134103">doi</a>.</li><li>Daza, Kent, Yung, \& Kuhn, Carbon Dioxide Conversion By Reverse Water-Gas Shift Chemical  Looping on Perovskite-Type Oxides, <i>{Industrial \& Engineering Chemistry Research}</i>, <b>53(14)</b>, 5828-5837 (2014). <a href="http://pubs.acs.org/doi/abs/10.1021/ie5002185">link</a>. <a href="http://dx.doi.org/10.1021/ie5002185">doi</a>.</li><li>Prateek Mehta, Paul Salvador \& John Kitchin, Identifying Potential \ce{BO2} Oxide Polymorphs for Epitaxial  Growth Candidates, <i>{ACS Appl. Mater. Interfaces}</i>, <b>6(5)</b>, 3630-3639 (2014). <a href="http://dx.doi.org/10.1021/am4059149">link</a>. <a href="http://dx.doi.org/10.1021/am4059149">doi</a>.</li><li>Suntivich, Hong, Lee, , Rondinelli, Yang, Goodenough, , Dabrowski, Freeland, Shao-Horn \& Yang, Estimating Hybridization of Transition Metal and Oxygen States  in Perovskites From O K-Edge X-Ray Absorption Spectroscopy, <i>{The Journal of Physical Chemistry C}</i>, <b>118(4)</b>, 1856-1863 (2014). <a href="http://pubs.acs.org/doi/abs/10.1021/jp410644j">link</a>. <a href="http://dx.doi.org/10.1021/jp410644j">doi</a>.</li><li>Alesi \& Kitchin, Evaluation of a Primary Amine-Functionalized Ion-Exchange  Resin for \ce{CO_2} Capture, <i>{Industrial \& Engineering Chemistry Research}</i>, <b>51(19)</b>, 6907-6915 (2012). <a href="http://dx.doi.org/10.1021/ie300452c">link</a>. <a href="http://dx.doi.org/10.1021/ie300452c">doi</a>.</li><li>Robert Day, Scientific English: A Guide for Scientists and Other Professionals, Oryx Press (1995).</li></ol>
#+END_HTML


bibliography:~/Dropbox/bibliography/references.bib