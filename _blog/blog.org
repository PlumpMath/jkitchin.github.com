* DONE Scoring elfeed articles
  CLOSED: [2017-01-05 Thu 11:18]
  :PROPERTIES:
  :categories: emacs,elfeed
  :date:     2017/01/05 11:18:29
  :updated:  2017/01/05 21:12:13
  :END:

I use [[https://github.com/skeeto/elfeed][elfeed]] to read RSS feeds of scientific journals, python, emacs, and lisp blogs, and the emacs stackexchange feed. Here are the current feeds I follow.

#+BEGIN_SRC emacs-lisp
(mapcar 'list elfeed-feeds)
#+END_SRC

#+RESULTS:
| (http://syndic8.scopus.com/getMessage?registrationId=ADEJAEEKHFESCDEOCDFOAHGSCDHSAKHREFMSADHNJA cmu)              |
| http://feeds.feedburner.com/acs/accacs                                                                            |
| http://feeds.feedburner.com/acs/enfuem                                                                            |
| http://feeds.feedburner.com/acs/esthag                                                                            |
| http://feeds.feedburner.com/acs/jacsat                                                                            |
| http://feeds.feedburner.com/acs/jpcbfk                                                                            |
| http://feeds.feedburner.com/acs/jpccck                                                                            |
| http://feeds.feedburner.com/acs/jpclcd                                                                            |
| http://feeds.feedburner.com/acs/cmatex                                                                            |
| http://feeds.feedburner.com/acs/jctcce                                                                            |
| http://feeds.feedburner.com/acs/jcisd8                                                                            |
| http://feeds.feedburner.com/acs/iecred                                                                            |
| http://feeds.aps.org/rss/recent/prl.xml                                                                           |
| http://feeds.aps.org/rss/recent/prb.xml                                                                           |
| http://www.sciencemag.org/rss/current.xml                                                                         |
| http://feeds.nature.com/nature/rss/current                                                                        |
| http://feeds.nature.com/nmat/rss/current                                                                          |
| http://feeds.nature.com/nchem/rss/current                                                                         |
| http://rss.sciencedirect.com/publication/science/09270256                                                         |
| http://onlinelibrary.wiley.com/rss/journal/10.1002/(ISSN)1521-3773                                                |
| http://scitation.aip.org/rss/content/aip/journal/jcp/latestarticles;jsessionid=6k76xb11z253.x-aip-live-06?fmt=rss |
| (http://planetpython.org/rss20.xml python)                                                                        |
| (http://planet.scipy.org/rss20.xml python)                                                                        |
| (http://planet.emacsen.org/atom.xml emacs)                                                                        |
| http://planet.lisp.org/rss20.xml                                                                                  |
| http://catalysis-preprint-archive.github.io/updates.rss                                                           |
| https://www.cmu.edu/policies/news/rss-feed.rss                                                                    |
| (http://emacs.stackexchange.com/feeds emacs)                                                                      |

I get a lot of articles this way. The current size of the database is:

#+BEGIN_SRC emacs-lisp
(elfeed-db-size)
#+END_SRC

#+RESULTS:
: 79721

Elfeed tells me I have over 300 unread entries to review at the moment.

#+BEGIN_SRC emacs-lisp
(elfeed-search--count-unread)
#+END_SRC

#+RESULTS:
: 341/363:24

To deal with this deluge, I have done a couple of things. I set up some new key-bindings so I can alternate marking entries as read if the titles do not look interesting. These keybindings let me alternate fingers, so they do not get too tired (that really happens some days!).

#+BEGIN_SRC emacs-lisp
;; help me alternate fingers in marking entries as read
(define-key elfeed-search-mode-map (kbd "f") 'elfeed-search-untag-all-unread)
(define-key elfeed-search-mode-map (kbd "j") 'elfeed-search-untag-all-unread)
#+END_SRC

I also set up some auto-tagging of the emacs and python feeds, and setup some custom faces so these tags are highlighted so they are easy to see. Anything highlighted in blue is related to emacs, green is related to python, and pink is related to my department, and I can type s, then the tag to see only those entries.  Here is what my feed looks like:

#+attr_org: :width 600
[[./screenshots/date-05-01-2017-time-08-42-33.png]]



Today I want to explore adding tags to entries to further prioritize them. There is a way to tag entries that is described here: https://github.com/skeeto/elfeed#tag-hooks where you can create patterns to match an entry feed title, url, title or link. Basically, you create a function that takes an entry, amd have it add or remove a tag conditionally.

I want to tag entries that meet certain criteria, for example keywords, and set a tag based on the number of matches. Ideally, one day this would be integrated with machine learning so it could rank entries by other entries I have liked, but today we setup code that will create a score for an entry based on the number of matches, and then tag it so that it will get highlighted for me.  First, we define two custom faces and setup elfeed to use them. I will use two tags: important and relevant. relevant will be for entries that get a score of at least 1, and important for entries that get a score greater than 1.

#+BEGIN_SRC emacs-lisp :results silent
(defface relevant-elfeed-entry
  `((t :background ,(color-lighten-name "orange1" 40)))
  "Marks a relevant Elfeed entry.")

(defface important-elfeed-entry
  `((t :background ,(color-lighten-name "OrangeRed2" 40)))
  "Marks an important Elfeed entry.")

(push '(relevant relevant-elfeed-entry)
      elfeed-search-face-alist)

(push '(important important-elfeed-entry)
      elfeed-search-face-alist)
#+END_SRC

In elfeed, each entry is a structure, and we can access the title and content for matching. Here is an example of a simple scoring function. The idea is just to match patterns, and then add to the score if it matches. This is not as advanced as [[https://www.gnu.org/software/emacs/manual/html_node/gnus/Scoring.html#Scoring][gnus scoring]], but it is a good starting point.

#+BEGIN_SRC emacs-lisp
(defun score-elfeed-entry (entry)
  (let ((title (elfeed-entry-title entry))
	(content (elfeed-deref (elfeed-entry-content entry)))
	(score 0))
    (loop for (pattern n) in '(("alloy" 1)
			       ("machine learning\\|neural" 1)
			       ("database" 1)
			       ("reproducible" 1)
			       ("carbon dioxide\\|CO2" 1)
			       ("oxygen evolution\\|OER\\|electrolysis" 1)
			       ("perovskite\\|polymorph\\|epitax" 1)
			       ("kitchin" 2))
	  if (string-match pattern title)
	  do (incf score n)
	  if (string-match pattern content)
	  do (incf score n))
    (message "%s - %s" title score)

    ;; store score for later in case I ever integrate machine learning
    (setf (elfeed-meta entry :my/score) score)

    (cond
     ((= score 1)
      (elfeed-tag entry 'relevant))
     ((> score 1)
      (elfeed-tag entry 'important)))
    entry))

(add-hook 'elfeed-new-entry-hook 'score-elfeed-entry)
#+END_SRC

#+RESULTS:
| score-elfeed-entry |

Now, new entries automatically get tagged with relevant or important, depending on the score that function gives them, and they get color-coded. Now, the feed looks like this:

#+attr_org: :width 600
[[./screenshots/date-05-01-2017-time-11-10-42.png]]


I saved some bookmarks to see just the important or relevant ones (http://nullprogram.com/blog/2015/12/03/) so I can see new relevant entries with C-x r b and selecting the relevant bookmark. These work from anywhere in Emacs.

#+BEGIN_EXAMPLE
  @6-months-ago +unread +relev  @6-months-ago +unread +relevant
  elfeed @6-months-ago +unread  @6-months-ago +unread +important
#+END_EXAMPLE

I usually access elfeed from a command that shows me everything. Here, I define key-bindings to show me just the important or relevant ones. I could not see a way to get an or in there to show me both of them. These keys make it a one key press to show only these entries, and then get back to the full list.

#+BEGIN_SRC emacs-lisp
(define-key elfeed-search-mode-map (kbd "i")
  (lambda () (interactive)
    (elfeed-search-set-filter "@6-months-ago +unread +important")))

(define-key elfeed-search-mode-map (kbd "v")
  (lambda () (interactive)
    (elfeed-search-set-filter "@6-months-ago +unread +relevant")))

(define-key elfeed-search-mode-map (kbd "c")
  (lambda () (interactive)
    (elfeed-search-set-filter "@6-months-ago +unread")))
#+END_SRC


That summarizes the experiment of the day. There is clearly some room for improvement on the scoring function, e.g. moving the patterns out of the function and into a customizable variable, making the patterns be specific to either the title or content, etc. I am going to try this for a few days and see if it is actually helpful first though.

* DONE Querying a MongoDB bibtex database with Python and emacs-lisp
  CLOSED: [2017-01-15 Sun 10:36]
  :PROPERTIES:
  :categories: database,mongodb,emacs,python
  :date:     2017/01/15 10:36:22
  :updated:  2017/01/15 10:36:22
  :END:
I have been exploring [[http://kitchingroup.cheme.cmu.edu/blog/2017/01/03/Find-stuff-in-org-mode-anywhere/][using databases]] to help with searching my data. In this post we explore using MongoDB for bibtex entries. I am choosing bibtex entries because it is easy to parse bibtex files, I already have a lot of them, and I have several kinds of queries I regularly use. So, they are a good candidate to test out a new database on!

MongoDB is a noSQL database that is pretty easy to use. I installed it from homebrew, and then followed the directions to run the server.

With pymongo you can make a database as easy as this:

#+BEGIN_SRC python :results output org drawer
import bibtexparser

# Read the bibtex file to get entries
with open('../../../Dropbox/bibliography/references.bib', 'r') as bibfile:
    bp = bibtexparser.load(bibfile)
    entries = bp.entries

print("N = ", len(entries))

print(entries[0])

import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

# This creates the "entries" collection
db = client['bibtex'].entries

# add each entry
for entry in entries:
    db.insert_one(entry)
#+END_SRC

#+RESULTS:
:RESULTS:
N =  1671
{'keyword': 'test, word', 'year': '2006', 'publisher': 'American Chemical Society (ACS)', 'title': 'The ACS Style Guide', 'ENTRYTYPE': 'book', 'editor': 'Janet S. Dodd', 'address': 'Washington, D.C.', 'ID': '2006-acs-style-guide', 'doi': '10.1021/bk-2006-styg', 'link': 'http://dx.doi.org/10.1021/bk-2006-STYG', 'date_added': 'Wed Apr  1 10:17:54 2015', 'pages': 'nil'}
:END:

That was easy. We have a database with 1671 documents in it, and each document is essentially a dictionary of key-value pairs. You might even argue it was too easy. I didn't specify any structure to the entries at all. No required fields, no validation that the keys are spelled correctly, no validation on the values, e.g. you can see the year looks like a string. The benefit of that is that every entry went in, with no issues. On the other hand, the authors went in as a single string, as did the keywords, which affects our ability to search a little bit later. Note if you run that twice, it will add each entry again, since we do not check if the entry already exists.

A database is only useful though if it is easy to get stuff out of it. So, let's consider some test queries. First we find entries that have years less than 1950. The query is basically a little json bundle that describes a field and condition that we want to match. Here we use a less than operator, ""$lt"The results come back as a list of dictionaries. This is in stark contrast to a SQL query which is an expression in its own declarative language. A query here is a chunk of data that must get converted to code by the server. I am not 100% clear if the less than here is in the string sense or numeric sense, but for years it probably does not matter for a long time.

#+BEGIN_SRC python :results output org drawer
import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

db = client['bibtex'].entries

for i, result in enumerate(db.find({"year" : {"$lt": "1950"}})):
    print('{i: 2d}. {author}, {title}, {journal}, {year}.'.format(i=i+1, **result))
#+END_SRC

#+RESULTS:
:RESULTS:
 1. Birch, Francis, Finite Elastic Strain of Cubic Crystals, Phys. Rev., 1947.
 2. Ditchburn, R. W. and Gilmour, J. C., The Vapor Pressures of Monatomic Vapors, Rev. Mod. Phys., 1941.
 3. J. Korringa, On the Calculation of the Energy of a Bloch Wave in a Metal, Physica, 1947.
 4. Nix, F. C. and MacNair, D., The Thermal Expansion of Pure Metals. {II}: Molybdenum, Palladium, Silver, Tantalum, Tungsten, Platinum, and Lead, Phys. Rev., 1942.
:END:


That seems easy enough, and those strings could easily be used as candidates for a selection tool like helm.

How about articles published by myself and my student Jacob Boes? This requires "and" logic. Apparently that is the default, so we just add three queries. One is an exact match on articles, and the other two are case-insensitive regular expression matches.  I guess this has to be done on every document, since there probably is no way to index a regex match! This search was very fast, but it is not clear how fast it would be for a million entries. This matching is necessary because we stored all authors in a single field rather than splitting them into an array. We might still have to match strings for this even in an array since an author might then be "John R. Kitchin", rather than further decomposed into first and last names.

#+BEGIN_SRC python :results output org drawer
import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

db = client['bibtex']
entries = db['entries']

for i, result in enumerate(entries.find({"ENTRYTYPE": "article",
                                         "author" : {"$regex": "kitchin", '$options' : 'i'},
                                         "author" : {"$regex": "boes", '$options' : 'i'}})):
    if result.get('doi', None):
        result['doi'] = 'http://dx.doi.org/{doi}'.format(doi=result['doi'])
    else:
        result['doi'] = ''
    print('{i: 2d}. {author}, {title}, {journal}, {year}. {doi}'.format(i=i+1, **result).replace("\n", ""))
#+END_SRC

#+RESULTS:
:RESULTS:
 1. Jacob R. Boes and Peter Kondratyuk and Chunrong Yin and JamesB. Miller and Andrew J. Gellman and John R. Kitchin, Core Level Shifts in {Cu-Pd} Alloys As a Function of BulkComposition and Structure, Surface Science, 2015. http://dx.doi.org/10.1016/j.susc.2015.02.011
 2. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Estimating Bulk-Composition-Dependent \ce{H2} AdsorptionEnergies on \ce{Cu_{x}Pd_{1-x}} Alloy (111) Surfaces, ACS Catalysis, 2015. http://dx.doi.org/10.1021/cs501585k
 3. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Supporting Information: Estimating Bulk-Composition-Dependent\ce{H2} Adsorption Energies on \ce{Cu_{x}Pd_{1-x}} Alloy (111)Surfaces, ACS Catalysis, 2015. http://dx.doi.org/10.1021/cs501585k
 4. G. Gumuslu and P. Kondratyuk and J. R. Boes and B. Morrealeand J. B. Miller and J. R. Kitchin and A. J. Gellman, Correlation of Electronic Structure With Catalytic Activity:\ce{H2}-\ce{D2} Exchange Across \ce{Cu_{x}Pd_{1-x}}Composition Space, ACS Catalysis, 2015. http://dx.doi.org/10.1021/cs501586t
 5. John D. Michael and Ethan L. Demeter and Steven M. Illes andQingqi Fan and Jacob R. Boes and John R. Kitchin, Alkaline Electrolyte and {Fe} Impurity Effects on thePerformance and Active-Phase Structure of {NiOOH} Thin Filmsfor {OER} Catalysis Applications, J. Phys. Chem. C, 2015. http://dx.doi.org/10.1021/acs.jpcc.5b02458
 6. Jacob R. Boes and Mitchell C. Groenenboom and John A. Keithand John R. Kitchin, Neural Network and {Reaxff} Comparison for {Au} Properties, Int. J. Quantum Chem., 2016. http://dx.doi.org/10.1002/qua.25115
 7. Jacob R. Boes and John R. Kitchin, Neural Network Predictions of Oxygen Interactions on a Dynamic Pd Surface, Molecular Simulation, Accepted 12/2016. http://dx.doi.org/10.1080/08927022.2016.1274984
 8. Jacob R. Boes and John R. Kitchin, Modeling Segregation on {AuPd}(111) Surfaces With DensityFunctional Theory and Monte Carlo Simulations, Submitted to J. Phys. Chem. C, 2016. 
:END:

We can find out how many different entry types we have, as well as how many distinct keyword entries there are. The documents do not separate the keywords though, so this is just the unique strings of comma-separated keywords values. We would have had to split those in advance to have a list of keywords to search for a specific one beyond string matching. Curiously, in my bibtex entries, these are in a field called "keywords". It appears the bibtex parser may have changed the name to "keyword".

#+BEGIN_SRC python :results output org drawer
import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

db = client['bibtex']
entries = db['entries']

print(entries.distinct("ENTRYTYPE"))
print(len(entries.distinct("keyword")))
print(entries.find({"keyword": {"$exists": "true"}})[22]['keyword'])
#+END_SRC

#+RESULTS:
:RESULTS:
['book', 'article', 'techreport', 'phdthesis', 'inproceedings', 'inbook', 'mastersthesis', 'misc', 'incollection']
176
Bildungsw{\"a}rmen, Dichtefunktionalrechnungen, Perowskite, Thermochemie
:END:

** text searching

You can do text search as well. You first have to create an index on one or more fields, and then use the $text and $search operators. Here I made an index on a few fields, and then searched on it. Note that you can only have one text index, so think about it in advance! This simplifies the query a bit, we do not have to use the regex syntax for matching on a field.

#+BEGIN_SRC python :results output org drawer
import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

db = client['bibtex']
entries = db['entries']

entries.create_index([('author', pymongo.TEXT),
                      ('title', pymongo.TEXT),
                      ('keyword', pymongo.TEXT)], sparse=True)

for i, result in enumerate(entries.find({"$text" : {"$search": "kitchin", "$search": "boes"}})):
    print('{i: 2d}. {author}, {title}, {journal}, {year}.'.format(i=i, **result).replace("\n", ""))
#+END_SRC

#+RESULTS:
:RESULTS:
 0. G. Gumuslu and P. Kondratyuk and J. R. Boes and B. Morrealeand J. B. Miller and J. R. Kitchin and A. J. Gellman, Correlation of Electronic Structure With Catalytic Activity:\ce{H2}-\ce{D2} Exchange Across \ce{Cu_{x}Pd_{1-x}}Composition Space, ACS Catalysis, 2015.
 1. Jacob R. Boes and Peter Kondratyuk and Chunrong Yin and JamesB. Miller and Andrew J. Gellman and John R. Kitchin, Core Level Shifts in {Cu-Pd} Alloys As a Function of BulkComposition and Structure, Surface Science, 2015.
 2. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Estimating Bulk-Composition-Dependent \ce{H2} AdsorptionEnergies on \ce{Cu_{x}Pd_{1-x}} Alloy (111) Surfaces, ACS Catalysis, 2015.
 3. Jacob R. Boes and John R. Kitchin, Neural Network Predictions of Oxygen Interactions on a Dynamic Pd Surface, Molecular Simulation, Accepted 12/2016.
 4. Jacob R. Boes and John R. Kitchin, Modeling Segregation on {AuPd}(111) Surfaces With DensityFunctional Theory and Monte Carlo Simulations, Submitted to J. Phys. Chem. C, 2016.
 5. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Supporting Information: Estimating Bulk-Composition-Dependent\ce{H2} Adsorption Energies on \ce{Cu_{x}Pd_{1-x}} Alloy (111)Surfaces, ACS Catalysis, 2015.
 6. John D. Michael and Ethan L. Demeter and Steven M. Illes andQingqi Fan and Jacob R. Boes and John R. Kitchin, Alkaline Electrolyte and {Fe} Impurity Effects on thePerformance and Active-Phase Structure of {NiOOH} Thin Filmsfor {OER} Catalysis Applications, J. Phys. Chem. C, 2015.
 7. Jacob R. Boes and Mitchell C. Groenenboom and John A. Keithand John R. Kitchin, Neural Network and {Reaxff} Comparison for {Au} Properties, Int. J. Quantum Chem., 2016.
:END:

We can use this to search for documents with orgmode in a keyword or title too.

#+BEGIN_SRC python :results output org drawer
import pymongo
from pymongo import MongoClient
client = MongoClient('localhost', 27017)

db = client['bibtex']
entries = db['entries']

entries.create_index([('author', pymongo.TEXT),
                      ('title', pymongo.TEXT),
                      ('keyword', pymongo.TEXT)], sparse=True)

for i, result in enumerate(entries.find({"$text" : {"$search": "orgmode"}})):
    print('{i: 2d}. {author}, {title}, {journal}, {year}.'.format(i=i, **result).replace("\n", ""))
#+END_SRC

#+RESULTS:
:RESULTS:
 0. John R. Kitchin, Data Sharing in Surface Science, Surface Science, 2016.
 1. Zhongnan Xu and John R. Kitchin, Probing the Coverage Dependence of Site and AdsorbateConfigurational Correlations on (111) Surfaces of LateTransition Metals, J. Phys. Chem. C, 2014.
 2. Xu, Zhongnan and Rossmeisl, Jan and Kitchin, John R., A Linear Response {DFT}+{U} Study of Trends in the OxygenEvolution Activity of Transition Metal Rutile Dioxides, The Journal of Physical Chemistry C, 2015.
 3. Prateek Mehta and Paul A. Salvador and John R. Kitchin, Identifying Potential \ce{BO2} Oxide Polymorphs for EpitaxialGrowth Candidates, ACS Appl. Mater. Interfaces, 2015.
 4. Xu, Zhongnan and Joshi, Yogesh V. and Raman, Sumathy andKitchin, John R., Accurate Electronic and Chemical Properties of 3d TransitionMetal Oxides Using a Calculated Linear Response {U} and a {DFT+ U(V)} Method, The Journal of Chemical Physics, 2015.
 5. Zhongnan Xu and John R. Kitchin, Relationships Between the Surface Electronic and ChemicalProperties of Doped 4d and 5d Late Transition Metal Dioxides, The Journal of Chemical Physics, 2015.
 6. Zhongnan Xu and John R Kitchin, Tuning Oxide Activity Through Modification of the Crystal andElectronic Structure: From Strain To Potential Polymorphs, Phys. Chem. Chem. Phys., 2015.
 7. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Supporting Information: Estimating Bulk-Composition-Dependent\ce{H2} Adsorption Energies on \ce{Cu_{x}Pd_{1-x}} Alloy (111)Surfaces, ACS Catalysis, 2015.
 8. Kitchin, John R., Examples of Effective Data Sharing in Scientific Publishing, ACS Catalysis, 2015.
 9. Curnan, Matthew T. and Kitchin, John R., Effects of Concentration, Crystal Structure, Magnetism, andElectronic Structure Method on First-Principles Oxygen VacancyFormation Energy Trends in Perovskites, The Journal of Physical Chemistry C, 2014.
 10. Kitchin, John R. and Van Gulick, Ana E. and Zilinski, Lisa D., Automating Data Sharing Through Authoring Tools, International Journal on Digital Libraries, 2016.
 11. Jacob R. Boes and Gamze Gumuslu and James B. Miller and AndrewJ. Gellman and John R. Kitchin, Estimating Bulk-Composition-Dependent \ce{H2} AdsorptionEnergies on \ce{Cu_{x}Pd_{1-x}} Alloy (111) Surfaces, ACS Catalysis, 2015.
 12. Zhongnan Xu and John R. Kitchin, Relating the Electronic Structure and Reactivity of the 3dTransition Metal Monoxide Surfaces, Catalysis Communications, 2014.
 13. Spencer D. Miller and Vladimir V. Pushkarev and AndrewJ. Gellman and John R. Kitchin, Simulating Temperature Programmed Desorption of Oxygen on{P}t(111) Using {DFT} Derived Coverage Dependent DesorptionBarriers, Topics in Catalysis, 2014.
 14. Hallenbeck, Alexander P. and Kitchin, John R., Effects of \ce{O_2} and \ce{SO_2} on the Capture Capacity of aPrimary-Amine Based Polymeric \ce{CO_2} Sorbent, Industrial \& Engineering Chemistry Research, 2013.
:END:

** Querying from emacs-lisp

It is hard to get too excited about this if it is not easy to query from emacs and get data in a form we can use in emacs ;) The json library allows us to convert lisp data structures to json pretty easily. For example:

#+BEGIN_SRC emacs-lisp
(require 'json)

(json-encode '((ENTRYTYPE . article)
	       (author . (($regex . kitchin)
			  ($options . i)))
	       (author . (($regex . boes)
			  ($options . i)))))
#+END_SRC

#+RESULTS:
: {"ENTRYTYPE":"article","author":{"$regex":"kitchin","$options":"i"},"author":{"$regex":"boes","$options":"i"}}

So, we can use an a-list syntax to build up the query. Then we can send it to mongo using mongoexport that will return a json string that we can read back into emacs to get lisp data. Here is an example that returns a query. We print the first element here.

#+BEGIN_SRC emacs-lisp
(pp
 (aref (json-read-from-string
	(shell-command-to-string
	 (format "mongoexport --quiet --jsonArray -d bibtex -c entries -q '%s'"
		 (json-encode '((ENTRYTYPE . article)
				(author . (($regex . kitchin)
					   ($options . i)))
				(author . (($regex . boes)
					   ($options . i))))))))
       0))
#+END_SRC

#+RESULTS:
#+begin_example
((_id
  ($oid . "5878d9644c114f59fe86cb36"))
 (author . "Jacob R. Boes and Peter Kondratyuk and Chunrong Yin and James\nB. Miller and Andrew J. Gellman and John R. Kitchin")
 (year . "2015")
 (title . "Core Level Shifts in {Cu-Pd} Alloys As a Function of Bulk\nComposition and Structure")
 (ENTRYTYPE . "article")
 (ID . "boes-2015-core-cu")
 (keyword . "DESC0004031, early-career")
 (volume . "640")
 (doi . "10.1016/j.susc.2015.02.011")
 (link . "http://dx.doi.org/10.1016/j.susc.2015.02.011")
 (issn . "0039-6028")
 (journal . "Surface Science")
 (pages . "127-132"))
#+end_example

That is pretty sweet, we get a lisp data structure we can use. We can wrap that into a reasonable looking function here:

#+BEGIN_SRC emacs-lisp 
(defun mongo-find (db collection query)
  (json-read-from-string
   (shell-command-to-string
    (format "mongoexport --quiet --jsonArray -d %s -c %s -q '%s'"
	    db collection (json-encode query)))))
#+END_SRC

#+RESULTS:
: mongo-find

Now we can use the function to query the database, and then format the results. Here we look at the example of articles with authors that match "kitchin" and "boes".

#+BEGIN_SRC emacs-lisp
(loop for counter from 1 for entry across
      (mongo-find "bibtex" "entries" '((ENTRYTYPE . article)
				       (author . (($regex . kitchin)
						  ($options . i)))
				       (author . (($regex . boes)
						  ($options . i)))))
      do
      (setq entry (append `(,(cons "counter" counter)) entry))
      ;; make sure we have a doi field.
      (if (assoc 'doi entry)
	  (push (cons "doi" (format "http://dx.doi.org/%s" (cdr (assoc 'doi entry)))) entry)
	(push (cons "doi" "") entry))
      concat
      (concat (replace-regexp-in-string
	       "\n" " "
	       (s-format "${counter}. ${author}, ${title} (${year}). ${doi}"
			 'aget entry)) "\n"))
#+END_SRC

#+RESULTS:
: 1. Jacob R. Boes and Peter Kondratyuk and Chunrong Yin and James B. Miller and Andrew J. Gellman and John R. Kitchin, Core Level Shifts in {Cu-Pd} Alloys As a Function of Bulk Composition and Structure (2015). http://dx.doi.org/10.1016/j.susc.2015.02.011
: 2. Jacob R. Boes and Gamze Gumuslu and James B. Miller and Andrew J. Gellman and John R. Kitchin, Estimating Bulk-Composition-Dependent \ce{H2} Adsorption Energies on \ce{Cu_{x}Pd_{1-x}} Alloy (111) Surfaces (2015). http://dx.doi.org/10.1021/cs501585k
: 3. Jacob R. Boes and Gamze Gumuslu and James B. Miller and Andrew J. Gellman and John R. Kitchin, Supporting Information: Estimating Bulk-Composition-Dependent \ce{H2} Adsorption Energies on \ce{Cu_{x}Pd_{1-x}} Alloy (111) Surfaces (2015). http://dx.doi.org/10.1021/cs501585k
: 4. G. Gumuslu and P. Kondratyuk and J. R. Boes and B. Morreale and J. B. Miller and J. R. Kitchin and A. J. Gellman, Correlation of Electronic Structure With Catalytic Activity: \ce{H2}-\ce{D2} Exchange Across \ce{Cu_{x}Pd_{1-x}} Composition Space (2015). http://dx.doi.org/10.1021/cs501586t
: 5. John D. Michael and Ethan L. Demeter and Steven M. Illes and Qingqi Fan and Jacob R. Boes and John R. Kitchin, Alkaline Electrolyte and {Fe} Impurity Effects on the Performance and Active-Phase Structure of {NiOOH} Thin Films for {OER} Catalysis Applications (2015). http://dx.doi.org/10.1021/acs.jpcc.5b02458
: 6. Jacob R. Boes and Mitchell C. Groenenboom and John A. Keith and John R. Kitchin, Neural Network and {Reaxff} Comparison for {Au} Properties (2016). http://dx.doi.org/10.1002/qua.25115
: 7. Jacob R. Boes and John R. Kitchin, Neural Network Predictions of Oxygen Interactions on a Dynamic Pd Surface (Accepted 12/2016). http://dx.doi.org/10.1080/08927022.2016.1274984
: 8. Jacob R. Boes and John R. Kitchin, Modeling Segregation on {AuPd}(111) Surfaces With Density Functional Theory and Monte Carlo Simulations (2016). 

Wow, that looks like a pretty lispy way to query the database and use the results. It is probably pretty easy to do similar things for inserting and updating documents. I will save that for another day.

** Summary thoughts

This is not an exhaustive study of Mongo for a bibtex database. It does illustrate that it is potentially useful. Imagine a group of users can enter bibtex entries, and then share them through a central server. Or you query the server for entries and then select them using helm/ivy. That is probably faster than parsing large bibtex files (note, in org-ref I already cache the files in parsed form for performance reasons!).

It would make sense to split the authors, and keywords in another version of this database. It also could make sense to have a field that is the bibtex string, and to do text search on that string. That way you get everything in the entry for searching, and an easy way to generate bibtex files without having to reconstruct them.

It is especially interesting to run the queries through emacs-lisp since we get the benefit of editing lisp code while writing the query, e.g. parenthesis navigation, less quoting, etc... and we get back lisp data that can be used to construct helm/ivy queries, or other emacs things. That makes this look competitive with emacsql at least for the syntax. I predict that there will be more posts on this in the future.
* DONE A simple emacs-lisp interface to CRUD operations in mongodb
  CLOSED: [2017-01-16 Mon 09:44]
  :PROPERTIES:
  :categories: database,mongodb,emacs,emacslisp
  :date:     2017/01/16 09:44:16
  :updated:  2017/01/16 09:44:16
  :END:

In this [[http://kitchingroup.cheme.cmu.edu/blog/2017/01/15/Querying-a-MongoDB-bibtex-database-with-Python-and-emacs-lisp/][post]] I showed that MongoDB is pretty easy to use for simple database applications. I showed a way to get data out of the database that is native to Emacs, but to use Mongo in emacs applications comfortably, it would be really helpful to be able to create, read, update and delete (CRUD) entries. There is a minimal interface to mongodb for emacs-lisp here: https://github.com/m2ym/mongo-el. From what I can see, it seems limited to simple, single queries, and it is written with advanced features of emacs-lisp I do not understand enough to extend it. In the last post, I showed an easy way to use mongoexport to get data from a query out of a mongo database. Here I explore a similar approach to round out the CRUD (create, read, update and delete) operations for using emacs-lisp to work with mongodb. This will enable Emacs to easily use MongoDB in applications.

We use the mongo cli with the --eval option, which allows you to run commands on the database. The basic idea is to generate the json we need from a lisp data structure, and use that json in mongo commands as needed. This sounds simple, but below you will see there are plenty of corners to take care of.

The goal here is to get something that is pretty functional. It will not be able to support all the capabilities of MongoDB and the options available in the cli.

** Inserting entries

Here we insert a document into the contacts collection of the contacts database. As in the Python example we considered earlier, this database is automatically created when we run this command. 

#+BEGIN_SRC emacs-lisp
(require 'json))
(let* ((json (json-encode '((first-name . "John")
			    (last-name . "Kitchin")
			    (email . "jkitchin@cmu.edu"))))
       (cmd (format "mongo 127.0.0.1/contacts --quiet --eval 'db.contacts.insert(%s)'"
		    json)))
  (shell-command-to-string cmd))
#+END_SRC

#+RESULTS:
: json

Here is a function we can use for inserting, and as you can see it works for multiple inserts too. There is a limit on how long the json string can be for this, so you cannot add too many entries at once with this. I do not know what the limit is, and suspect it is related to using a shell command. When this succeeds there is data returned about what happened, which we try to get in lisp form. Also, I noticed I had to do a little bit of escaping, especially for entries containing a single quote, which messes up the quoting on the shell command, and for non-ascii characters which the shell did not handle well. Maybe this could be avoided with a file-based approach, or if we used a pipe to a process.

#+BEGIN_SRC emacs-lisp
(defun mongo-insert (db collection document)
  "Insert into DB.COLLECTION the DOCUMENT.
DOCUMENT will be some lisp structure that is converted to json."
  ;; we have to escape quote any single quotes. This came from
  ;; http://stackoverflow.com/questions/1250079/how-to-escape-single-quotes-within-single-quoted-strings
  (let* ((json (replace-regexp-in-string "'" "'\"'\"'" (json-encode document)))
	 ;; it seems utf-8 characters may cause issues. Let's just remove them.
	 (json (replace-regexp-in-string "[^[:ascii:]]" "" json))
	 (cmd (format "mongo %s --quiet --eval 'db.%s.insert(%s)'"
		      db collection
		      json))
	 (output (shell-command-to-string cmd)))
    (cond
     ((string-match "BulkWriteResult(" output)
      (json-read-from-string (substring output 16 -2)))
     ((string-match "WriteResult(" output)
      (json-read-from-string (substring output 12 -2)))
     (t
      output))))
#+END_SRC

#+RESULTS:
: mongo-insert

Here it is in action.
#+BEGIN_SRC emacs-lisp
(mongo-insert "contacts" "contacts"
	      '(((first-name . "John")
		 (last-name . "Kitchin")
		 (email . "jkitchin@cmu.edu"))
		((first-name . "Someone")
		 (last-name . "Else")
		 ("email" . "someone@out.there"))))
#+END_SRC

#+RESULTS:
: ((writeErrors . []) (writeConcernErrors . []) (nInserted . 2) (nUpserted . 0) (nMatched . 0) (nModified . 0) (nRemoved . 0) (upserted . []))

Seems like an ok way to get data from Emacs into a Mongo DB, and we get lisp data returned telling us what happened.

** Finding a document

To update documents we need to find them. We would like to find a document by the _id, but we have a small dilemma. The json we need for that needs to look like: {"_id": ObjectId("587babfaef131d0d4603b3ad")}, where the ObjectId is not quoted. The json library does not seem to be able to do that. So, we have to modify our find code to do this. This is possible by manipulating the json string after it is generated with regular expression replacement. It feels hacky, and hopefully there are not many more examples of that. If there are, we will need another approach to generating the json data. Here is the modified find function, also with the projection option. Here is another place we have to tread somewhat lightly with the _id, in this case we have to requote it so that it can be read by emacs. It might make sense to just replace it with the quoted _id string, rather than the ObjectId call. Time will tell.

Here we create two helper functions to unquote input, and requote output. We also need some code to make an array of all the results, and put commas between all the results so that we end up with valid json in the output.

#+BEGIN_SRC emacs-lisp
(defun mongo-unquote-query (query)
  "Json encodes QUERY, and unquotes any ObjectId calls.

We don't have syntax for the ObjectId call that mongo wants in
 lisp, so a query has to look like this:
'((_id .  \"ObjectId(\"587babfaef131d0d4603b3ad\")\"))

Mongo can't have the quotes around the call, so this function
removes them.
"
  (replace-regexp-in-string "\"\\(ObjectID(\\\\\"\\(.*?\\)\\\\\")\\)\""
			    "ObjectId(\"\\2\")"
			    (json-encode query)))

(defun mongo-requote-output (output)
  "Adds quotes around ObjectId in OUTPUT.
When mongo outputs json, it has unquoted ObjectIds in it that
emacs cannot interpret as json. "
  (replace-regexp-in-string
   "ObjectId(\"\\(.*?\\)\")"
   "\"ObjectId(\\\\\"\\1\\\\\")\""
   output))

(defun mongo-find (db collection query &optional projection)
  (let* ((query-json (mongo-unquote-query query))
	 (projection-json
	  (and projection (json-encode projection)))
	 (output (mongo-requote-output
		  ;; add [] to make an array of output in json,
		  ;; and separate results by a comma
		  (concat "["
			  (replace-regexp-in-string
			   "\n" ""
			   (shell-command-to-string
			    (format "mongo %s --quiet --eval 'db.%s.find(%s).forEach(function(myDoc) { printjsononeline(myDoc); print( \",\"); })'"
				    db collection
				    (if projection
					(format "%s, %s" query-json projection-json)
				      query-json))))
			  "]")))) 
    (json-read-from-string output)))
#+END_SRC

#+RESULTS:
: mongo-find

So, finally we can run something like this:

#+BEGIN_SRC emacs-lisp
(mongo-find "contacts" "contacts" '((email . "someone@out.there")))
#+END_SRC

#+RESULTS:
: [((_id . "ObjectId(\"587c166cdfcd649d3acf99fd\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c16ad410565dd4c16c748\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c17550e586b4f8df21de0\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c1764d75279a55ffec483\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c17743281f1e9d5054396\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c178ad92706d2bd5a6e3c\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there")) ((_id . "ObjectId(\"587c1794756bb2bd0f0ac499\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there"))]

Here is an example usage with a projection that returns only the information you want, in this case, just the id.

#+BEGIN_SRC emacs-lisp
(mongo-find "contacts" "contacts" '((email . "someone@out.there"))
	    '((_id . 1)))
#+END_SRC

#+RESULTS:
: [((_id . "ObjectId(\"587c166cdfcd649d3acf99fd\")")) ((_id . "ObjectId(\"587c16ad410565dd4c16c748\")")) ((_id . "ObjectId(\"587c17550e586b4f8df21de0\")")) ((_id . "ObjectId(\"587c1764d75279a55ffec483\")")) ((_id . "ObjectId(\"587c17743281f1e9d5054396\")")) ((_id . "ObjectId(\"587c178ad92706d2bd5a6e3c\")")) ((_id . "ObjectId(\"587c1794756bb2bd0f0ac499\")"))]

** Updating an entry

Ok, back to the update. To make sure that we update exactly the document we want, we will use the document _id. First, we define an update command.

#+BEGIN_SRC emacs-lisp
(defun mongo-update (db collection query $set)
  "In DB.COLLECTION update records matching QUERY with the contents of $SET."
  (let* ((query-json (mongo-encode-query query))
	 ($set-json (mongo-encode-query $set))
	 (cmd (format "mongo %s --quiet --eval 'db.%s.update(%s, %s)'"
		      db collection
		      query-json $set-json))
	 (output (shell-command-to-string cmd)))
    (if (string-match "WriteResult(" output)
	(json-read-from-string
	 (substring output 12 -2))
      output)))
#+END_SRC

#+RESULTS:
: mongo-update

First a reminder of what is in this record.
#+BEGIN_SRC emacs-lisp
(mongo-find "contacts" "contacts" '((_id . "ObjectId(\"587c16ad410565dd4c16c748\")")))	    
#+END_SRC

#+RESULTS:
: [((_id . "ObjectId(\"587c16ad410565dd4c16c748\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there"))]

Here we set the email field to a new address. Without $set, the whole document gets replaced.

#+BEGIN_SRC emacs-lisp
(mongo-update "contacts" "contacts"
	      '((_id . "ObjectId(\"587c16ad410565dd4c16c748\")"))
	      '(($set . ((email . "someone@out.there.com")))))
#+END_SRC

#+RESULTS:
: ((nMatched . 1) (nUpserted . 0) (nModified . 1))

Finally, let's see the document again to verify it is modified.

#+BEGIN_SRC emacs-lisp
(mongo-find "contacts" "contacts" '((_id . "ObjectId(\"587c16ad410565dd4c16c748\")")))	    
#+END_SRC

#+RESULTS:
: [((_id . "ObjectId(\"587c16ad410565dd4c16c748\")") (first-name . "Someone") (last-name . "Else") (email . "someone@out.there.com"))]

Looks good, you can see it got changed. There is a potential gotcha though. This next command looks like it should do the same thing, but it does not. The whole document gets replaced!

#+BEGIN_SRC emacs-lisp
(mongo-update "contacts" "contacts"
	      '((_id . "ObjectId(\"587c16ad410565dd4c16c748\")"))
	      '((email . "someone@out.there.com")))
#+END_SRC

#+RESULTS:
: ((nMatched . 1) (nUpserted . 0) (nModified . 1))

#+BEGIN_SRC emacs-lisp
(mongo-find "contacts" "contacts" '((_id . "ObjectId(\"587c16ad410565dd4c16c748\")")))	    
#+END_SRC

#+RESULTS:
: [((_id . "ObjectId(\"587c16ad410565dd4c16c748\")") (email . "someone@out.there.com"))]

Do not forget the $set operator if you just want to update some fields!

** Deleting a document

Next, let's get a delete function. I will only implement the deleteMany function here since you can give it a document id to delete only one, and usually I would want to delete all documents that meet a criteria anyway.

#+BEGIN_SRC emacs-lisp
(defun mongo-deleteMany (db collection filter)
  "Delete records in DB.COLLECTION matched by FILTER.
TODO: add write concern."
  (let* ((filter-json (mongo-encode-query filter)) 
	 (cmd (format "mongo %s --quiet --eval 'db.%s.deleteMany(%s)'"
		      db collection
		      filter-json))
	 (output (shell-command-to-string cmd)))
    (json-read-from-string output)))
#+END_SRC

#+RESULTS:
: mongo-deleteMany

Since we borked that last document, let's just delete it.

#+BEGIN_SRC emacs-lisp
(mongo-deleteMany "contacts" "contacts" '((_id . "ObjectId(\"587be3fa6009a569a277b680\")")))
#+END_SRC

#+RESULTS:
: ((acknowledged . t) (deletedCount . 0))

** Generic commands

We may want some flexibility to run collection commands.  The most generic command will simply be to write the shell-command completely. We can keep a little syntax by encapsulating most of the boilerplate though. Here is a function for that.

#+BEGIN_SRC emacs-lisp
(defun mongo-cmd (db collection cmd &rest args)
  "In DB.COLLECTION run CMD. 
ARGS if present will be used to format CMD."
  (shell-command-to-string
   (format "mongo %s --quiet --eval 'db.%s.%s'"
	   db collection
	   (apply #'format cmd args))))
#+END_SRC

#+RESULTS:
: mongo-cmd

We can get the number of documents with this:

#+BEGIN_SRC emacs-lisp
(mongo-cmd "contacts" "contacts" "count()")
#+END_SRC

#+RESULTS:
: 4341

Or run a more sophisticated command with arguments like this.

#+BEGIN_SRC emacs-lisp
(mongo-cmd "contacts" "contacts" "explain().remove(%s)" (json-encode '(("category" . "enemy"))))
#+END_SRC

#+RESULTS:
#+begin_example
{
	"queryPlanner" : {
		"plannerVersion" : 1,
		"namespace" : "contacts.contacts",
		"indexFilterSet" : false,
		"parsedQuery" : {
			"category" : {
				"$eq" : "enemy"
			}
		},
		"winningPlan" : {
			"stage" : "DELETE",
			"inputStage" : {
				"stage" : "COLLSCAN",
				"filter" : {
					"category" : {
						"$eq" : "enemy"
					}
				},
				"direction" : "forward"
			}
		},
		"rejectedPlans" : [ ]
	},
	"serverInfo" : {
		"host" : "Johns-MacBook-Air.local",
		"port" : 27017,
		"version" : "3.4.1",
		"gitVersion" : "5e103c4f5583e2566a45d740225dc250baacfbd7"
	},
	"ok" : 1
}
#+end_example

Or, drop the collection with:

#+BEGIN_SRC emacs-lisp
(mongo-cmd "contacts" "contacts" "drop()")
#+END_SRC

#+RESULTS:
: true

All gone! Note, we do not try to handle the output of any of those, and they are returned as strings.

** A MongoDB contacts database

Now, let's re-populate it for real. I store my contacts in a variable called "contacts" as a list of a descriptive string and then cons cells. These are actually harvested from a set of org-files. It is way to slow to parse these files each time, so I keep the contacts cached in memory and only update them if a file changes.

#+BEGIN_SRC emacs-lisp
(length contacts)
#+END_SRC

#+RESULTS:
: 6047

There are over 6000 contacts. Let's put them in a MongoDB.

Here is a limitation of our approach. This will not work because the generated shell command ends up being too long for the shell.

#+BEGIN_SRC emacs-lisp
(mongo-insert "contacts" "contacts"
	      (loop for contact in contacts
		    collect
		    (append `((desc . ,(car contact))) (cdr contact))))
#+END_SRC

#+RESULTS:

So, we do them one at time here:

#+BEGIN_SRC emacs-lisp
(let ((ct (current-time)))
  (loop for contact in contacts
	do
	(let ((output (mongo-insert "contacts" "contacts"
				    (append `((desc . ,(car contact))) (cdr contact)))))
	  (unless (= 1 (cdr (assoc 'nInserted output)))
	    (warn "error: %S for %S" (cdr (assoc 'nInserted output)) contact))))
  (message "Elapsed time %.02f seconds" (float-time (time-since ct))))
#+END_SRC

#+RESULTS:
: Elapsed time 762.95 seconds

That took a little over 10 minutes to add. That seems long to me. This next step confirms that they were added.

#+BEGIN_SRC emacs-lisp
(mongo-cmd "contacts" "contacts" "count()")
#+END_SRC

#+RESULTS:
: 6047

Next we will compare some timing of finding data in the database vs looping through the cached contacts. Here is a timing macro to measure how long it takes to run a bit of code.

#+BEGIN_SRC emacs-lisp
;; http://stackoverflow.com/questions/23622296/emacs-timing-execution-of-function-calls-in-emacs-lisp
(defmacro measure-time (&rest body)
  "Measure the time it takes to evaluate BODY."
  `(let ((time (current-time)))
     ,@body
     (message "%.06f seconds elapsed" (float-time (time-since time)))))
#+END_SRC

#+RESULTS:
: measure-time

Here is the old way I would extract data. Many contacts I have are academics, and I have stored their academic ranks in each contact.

#+BEGIN_SRC emacs-lisp
(loop for contact in contacts
      if (string= "Professor" (cdr (assoc "RANK" (cdr contact))))
      collect contact into professors
      if (string= "Associate Professor" (cdr (assoc "RANK" (cdr contact))))
      collect contact into associate-professors
      if (string= "Assistant Professor" (cdr (assoc "RANK" (cdr contact))))
      collect contact into assistant-professors
      finally return `(("Assistant Professor" ,(length assistant-professors))
		       ("Associate Professor" ,(length associate-professors))
		       ("Professor" ,(length professors))))
#+END_SRC

#+RESULTS:
| Assistant Professor | 313 |
| Associate Professor | 283 |
| Professor           | 879 |

How long did it take to do that?

#+BEGIN_SRC emacs-lisp
(measure-time
 (loop for contact in contacts
       if (string= "Professor" (cdr (assoc "RANK" (cdr contact))))
       collect contact into professors
       if (string= "Associate Professor" (cdr (assoc "RANK" (cdr contact))))
       collect contact into associate-professors
       if (string= "Assistant Professor" (cdr (assoc "RANK" (cdr contact))))
       collect contact into assistant-professors
       finally return (list (length assistant-professors)
			    (length associate-professors)
			    (length professors))))
#+END_SRC

#+RESULTS:
: 0.008772 seconds elapsed

Not long at all! Comparatively, it is very slow to get this information out of the mongodb, although considerably less code is required. That might not be surprising, considering the json parsing that has to get done here.

Here is the equivalent code to extract that data from the database.

#+BEGIN_SRC emacs-lisp
(loop for rank in '("Assistant Professor" "Associate Professor" "Professor")
       collect (list rank (length (mongo-find "contacts" "contacts"
					      `((RANK . ,rank))))))
#+END_SRC

#+RESULTS:
| Assistant Professor | 313 |
| Associate Professor | 283 |
| Professor           | 879 |

It is comparatively slow to do this. This requires three json parses, and profiling indicates that alot of the work is done in parsing the json. 

#+BEGIN_SRC emacs-lisp
(measure-time
 (loop for rank in '("Assistant Professor" "Associate Professor" "Professor")
       collect (list rank (length (mongo-find "contacts" "contacts"
					      `((RANK . ,rank)))))))
#+END_SRC

#+RESULTS:
: 1.914817 seconds elapsed

Here is smarter way to do it that avoids the json parsing.

#+BEGIN_SRC emacs-lisp
(loop for rank in '("Assistant Professor" "Associate Professor" "Professor")
      collect (list rank (mongo-cmd "contacts" "contacts" "count(%s)"
				    (json-encode `((RANK . ,rank))))))
#+END_SRC

#+RESULTS:
| Assistant Professor | 313 |
| Associate Professor | 283 |
| Professor           | 879 |

And you can see here it is about 10 times faster, but not nearly as fast as running the lisp code on the cache.

#+BEGIN_SRC emacs-lisp
(measure-time
 (loop for rank in '("Assistant Professor" "Associate Professor" "Professor")
       collect (list rank (mongo-cmd "contacts" "contacts" "count(%s)"
				     (json-encode `((RANK . ,rank)))))))
#+END_SRC

#+RESULTS:
: 0.349413 seconds elapsed

This is how you might integrate this into a completion command:

#+BEGIN_SRC emacs-lisp
(ivy-read "choose: "
	  (loop for c across (mongo-find "contacts" "contacts" "")
		collect
		(list (cdr (assoc 'desc c)) c)))  
#+END_SRC

This is basically unusable though, because it takes so long to generate the candidates (over six seconds).

#+BEGIN_SRC emacs-lisp
(measure-time
 (loop for c across (mongo-find "contacts" "contacts" "")
       collect
       (list (cdr (assoc 'desc c)) c)))
#+END_SRC

#+RESULTS:
: 6.228225 seconds elapsed

We can get back to usable by making the database do more work for us. Here, we simply make the database print a list of cons cells that we can read into lisp. We have to use a javascript function, with some escaping and quoting. The escaping was necessary because there is some bad data in the email field that messed up the cons cells, e.g. some things like "name" <email> with nested single and double quoting, etc., and the quoting was necessary to get cons cells of the form ("desc" . "email"), and finally we wrap them in parentheses and read back the list of cons cells. At about a quarter of a second, this is very usable to get a list of over 6000 candidates. It is still many times slower than working on the contacts list in memory though. I am not a super fan of the one-line javascript, and if it was much more complicated than this another strategy would probably be desirable.

#+BEGIN_SRC emacs-lisp
(measure-time
 (read
  (concat
   "("
   (shell-command-to-string "mongo contacts --quiet --eval 'db.contacts.find().forEach(function (doc) {print(\"(\\\"\" + doc.desc + \"\\\" . \\\"\" + escape(doc.EMAIL) +\"\\\")\");})'")
   ")")))
#+END_SRC

#+RESULTS:
: 0.284730 seconds elapsed

** Text searching

Finally, let us make a text index to make searching easy. This allows us a very flexible search where we do not have to specify what field or use regular expressions. We setup the index on all the fields, so we can find entries that match even on fields that do not exist in all documents.

#+BEGIN_SRC emacs-lisp
(mongo-cmd "contacts" "contacts" "createIndex(%s)" (json-encode '(("$**" . "text"))))
#+END_SRC

#+RESULTS:
: {
: 	"createdCollectionAutomatically" : false,
: 	"numIndexesBefore" : 1,
: 	"numIndexesAfter" : 2,
: 	"ok" : 1
: }

Now, let's use that to find the GOOGLE-SCHOLAR url of contacts matching the following query.

#+BEGIN_SRC emacs-lisp :results code
(mongo-find "contacts" "contacts" '(($text . (($search . "\"Carnegie Mellon\"")))
				    ($text . (($search . "\"John Kitchin\""))))
	    '((GOOGLE-SCHOLAR . 1) (_id . 0)))
#+END_SRC

#+RESULTS:
#+BEGIN_SRC emacs-lisp
[((GOOGLE-SCHOLAR . "https://scholar.google.com/citations?hl=en&user=jD_4h7sAAAAJ"))
 nil nil]
#+END_SRC

So, you can see there were three contacts, and one of them lists my google-scholar url.


** Summary

This looks like the foundation of a mongo/emacs-lisp interface. This interface is not that fast though, and suffers from some limitations related to the use of the shell. Depending on the actual use, it is clear you can gain performance by passing some work on the database, which requires some javascript coding. Even that revealed some subtlety, e.g. making sure the database output text that was compatible with the lisp reader. That mostly means taking care of quotes, and other special characters, which I managed with a simple escape mechanism. It is probably worth investing a few more days in building an interface that uses a process and communicates with the mongo cli directly before moving forward with any significant application that uses Mongo in emacs. There are many good ideas for that:

1. Index all your org files (e.g. http://kitchingroup.cheme.cmu.edu/blog/2017/01/03/Find-stuff-in-org-mode-anywhere/)
2. Index all your bibtex files (e.g. http://kitchingroup.cheme.cmu.edu/blog/2017/01/15/Querying-a-MongoDB-bibtex-database-with-Python-and-emacs-lisp/)
3. Download RSS feeds into a searchable database
4. Manage your contacts
5. Index your email? mu and notmuch use xapian for this, but I have found they cannot search for things like hashtags. Maybe MongoDB would be better?

The tradeoffs between this and sqlite are more clear now. With Mongo we do not have to create the normalized tables (although it is still a good idea to think about how to structure the documents, and if they should be a little normalized). It is /much/ easier to map lisp data structures to Mongo queries than it is to do that with SQL queries. On the other hand, it is necessary to do some javascript programming with Mongo to get some desired output. It still seems worth exploring further.
* Track changes with overlays
  :PROPERTIES:
  :ID:       E2A816F1-8293-457C-8945-18ABF636839D
  :END:

the idea here is to use overlays to mark text that is deleted or inserted.

[[file:~/vc/jkitchin-github/scimax/elpa/cm-mode-20160310.829/cm-mode.el::;;%20Keywords:%20text,%20markdown]]

These are snippets{-- that--} make track changes work in cm-mode with some document markup

test{++ ok++}
#+BEGIN_SRC emacs-lisp
;; activate
(progn
  (add-to-list 'before-change-functions 'cm-before-change t)
  (add-to-list 'after-change-functions 'cm-after-change)
  (setq cm-follow-changes t)
  (message "Follow changes mode activated."))

;; deactivate
(progn
  (setq before-change-functions (delq 'cm-before-change before-change-functions))
  (setq after-change-functions (delq 'cm-after-change after-change-functions))
  (setq cm-follow-changes nil)
  (message "Follow changes mode deactivated."))

(defun cm-before-change (beg end)
  "Function to execute before a buffer change."
  (unless (or undo-in-progress
              (and (= beg (point-min)) (= end (point-max)))) ; this happens on buffer switches
    (if (= beg end)                   ; addition
        (cm-make-addition (cm-markup-at-point))
      ;; when the deletion was done with backspace, point is at end.
      (setq cm-current-deletion (list (buffer-substring beg end) (= (point) end))))))

(defun cm-after-change (beg end length)
  "Function to execute after a buffer change.
This function marks deletions. See cm-before-change for
details."
  (unless (or undo-in-progress
              (not cm-current-deletion))
    (apply 'cm-make-deletion cm-current-deletion)
    (setq cm-current-deletion nil)))
#+END_SRC


help:before-change-functions 
Documentation:
List of functions to call before each text change.
Two arguments are passed to each function: the positions of
the beginning and end of the range of old text to be changed.
(For an insertion, the beginning and end are at the same place.)
No information is given about the length of the text after the change.

help:after-change-functions
Documentation:
List of functions to call after each text change.
Three arguments are passed to each function: the positions of
the beginning and end of the range of changed text,
and the length in chars of the pre-change text replaced by that range.
(For an insertion, the pre-change length is zero;
for a deletion, that length is the number of chars deleted,
and the post-change beginning and end are a#  ov-highlight-data: KCgxMjAwMCAxMjAwMCAob3YtdHlwZSAiZGVsZXRlIiBvdi1oaWdobGlnaHQgdCBmYWNlICg6Zm9yZWdyb3VuZCAicmVkIiA6c3RyaWtlLXRocm91Z2ggdCkpKSAoMTIwMDAgMTIwMDAgKGZhY2UgKDpmb3JlZ3JvdW5kICJyZWQiIDpzdHJpa2UtdGhyb3VnaCB0KSBvdi1oaWdobGlnaHQgdCkpKQ==

(setq inhibit-modification-hooks nil
(ovh-track-changes)
#+BEGIN_SRC emacs-lisp
(add-to-list 'before-change-functions 'ovh-before-change t)
(add-to-list 'after-change-functions 'ovh-after-change)
#+END_SRC

#+RESULTS
| ovh-after-change |




# Local Variables:
# eval: (ov-highlight-load)
# End:

#+RESULTS:
| ovh-after-change |
* Superpowered email in Emacs

Gmail does a few things right: awesome search, email completion. I am not a big fan of writing email in a browser though. You know, because I like editing text in Emacs.

I treat email primarily as an inbox of messages I need to do something with. My preference is to read an email, and either respond and archive, archive or delete it. If I can not do one of those things, the email should go on a todo list for me to handle later. If a message is in the inbox, it means it is waiting for me to do something. I try to keep under 50 emails in my inbox at any given time, and those are emails I usually plan to handle within a few days (or it goes on a todo list).

** TODO Attachment reminder
** Highlight some words from specific users

I get some automated reports from a computer cluster. These reports tell me if the RAID array is in Optimal, Degraded or Missing status. To make it easier to read these, I want those words to be highlighted in color. I especially want words like Degraded and Missing to be red so they stand out! This snippet highlights those words, but only in emails from the cluster. This is an example of conditionally changing the appearance of an email.

#+BEGIN_SRC emacs-lisp :results silent
(defun gilgamesh-fontify ()
  (loop for (name . email) in (mu4e-message-field-at-point :from) 
	do
	(when (string= email "root@gilgamesh.cheme.cmu.edu")
	  (hi-lock-mode 1)
	  (highlight-regexp "Optimal" 'mu4e-modeline-face) ; a greenish color
	  (highlight-regexp "Degraded" 'mu4e-warning-face) ; a reddish color
	  (highlight-regexp "Missing" 'mu4e-warning-face))))

(add-hook 'mu4e-view-mode-hook 'gilgamesh-fontify)
#+END_SRC

** Mail merge

Many times I collect information from people by email. I like to send individual emails, rather than one email to the whole group. It makes it simpler for me to aggregate the data they send me, and I get individual email threads. If the information is sufficiently structured, it is easy to use the mail-merge feature of scimax to generate and send the emails. 

Mail merge is a multi-step process

1. Create a data source (usually a table) that has all the information you need. Each row in the table will end up being one email. Give the table a name, so it can be the input for a source block later.

#+name: mail-merge-data
| email              | first name | last name | file       |
|--------------------+------------+-----------+------------|
| some@person.com    | Some       | One       | tees.org   |
| another@person.com | Another    | Person    | shorts.org |

2. Create a template using ${identifier} tags that will be replaced by things in the template by data from the data-source using s-format. I like to put these in a named example block like this so it is easy to include in a source block later. This block uses a personal name to address the person, tells them to check on a file, and attaches the file to the email. Note this also puts an id link in the message. If they reply, and that link is there, I can type C-c o on it and jump back to the heading where I sent the message from. I might copy some data from the message there.

#+name: email-template
#+BEGIN_EXAMPLE
Dear ${NAME},

Please check this file: ${FILE}. It is attached.

-----------------------
Please do not delete this.
[[id:${ID}]]

<#part type="application/octet-stream" filename="${PATH}" disposition=attachment>
<#/part>
#+END_EXAMPLE

3. Generate the messages using `mail-merge-make-headings'. You need to create a new data-source that defines the fields in the template, and the heading properties TO and SUBJECT. This will generate a subheading called Messages in the current heading. Each message will be a subsubheading, and initially tagged :unsent:.

#+BEGIN_SRC emacs-lisp :var data=mail-merge-data template=email-template
(let ((data-source (loop for (email fname lname file) in data
			 collect
			 `(("TO" . ,email)
			   ("SUBJECT" . ,(format "Meeting notes about %s" file))
			   ("HEADLINE" . ,(format "Message to %s %s" fname lname))
			   ("NAME" . ,fname)
			   ("FILE" . ,file)
			   ("PATH" . ,(expand-file-name file))))))
  (mail-merge-make-headings template data-source)))
#+END_SRC

#+RESULTS:

4. Inspect each message. You can edit these messages by hand, or modify the template/code above and re-run them. If a message is good, send it with M-x mail-merge-send-heading (which opens a message buffer and you have to C-c C-c to send it). I always do this for the first few to make sure the messages are formatted correctly. Once I am sure of that, C-u M-x mail-merge-send-heading will send the message directly. Both tag the message as sent, and add some properties about when the message was sent, stores a link to the sent message, and  move the point to the next unsent message. I actually use speed keys for this. When you are on the first star of the message headline, pressing m will open the message buffer for you to send, and pressing s will just send it. If you are super confident, you can use M-x mail-merge and it will send all headlines that are tagged unsent with a TO property with a 0.2 second delay.




*** Messages
**** DONE Message to Some One                                          :sent:
     CLOSED: [2017-01-07 Sat 15:17]
     :PROPERTIES:
     :ID: 35BD9490-4380-4062-A445-AF14718584FD
     :TO:       some@person.com
     :SUBJECT:  Meeting notes
     :SENT-ON:  Sat Jan  7 15:18:07 2017
     :Message-ID: [[mu4e:msgid:m2pojyfwua.fsf@Johns-MacBook-Air.local][Meeting notes (Sat Jan  7 15:18:07 2017)]
     :END:
Dear Some,

Please check this file: tees.org. It is attached.

-----------------------
Please do not delete this.
[[id:35BD9490-4380-4062-A445-AF14718584FD]]


**** Message to Another Person :sent:
     :PROPERTIES:
     :ID: A32AF98B-DC16-48BE-88FA-A3B7C879D931
     :TO:       another@person.com
     :SUBJECT:  Meeting notes
     :SENT-ON:  Sat Jan  7 15:17:48 2017
     :Message-ID: [[mu4e:msgid:m2shoufwut.fsf@Johns-MacBook-Air.local][Meeting notes (Sat Jan  7 15:17:48 2017)]]
     :END:
Dear Another,

Please check this file: shorts.org. It is attached.

-----------------------
Please do not delete this.
[[id:A32AF98B-DC16-48BE-88FA-A3B7C879D931]]

<#part type=\"application/octet-stream\" filename=\"/Users/jkitchin/vc/blogofile-jkitchin.github.com/_blog/shorts.org\" disposition=attachment>
<#/part>

** Triggering followup actions on replies

A sent message has a message-id and when people reply to it, that id should be in a references header field. We can set up a hook that alerts us when someone replies to a specific email. This will be triggered on every view, so you might be careful about what the action does.

#+BEGIN_SRC emacs-lisp
(setq email-notifications '(("m28tqmhobn.fsf@Johns-MacBook-Air.local" . (message-box "Followup!"))))

(defun check-for-followup ()
  (loop for (ref . action) in email-notifications
	do
	(message "%s: %S against %S"
		 (-contains? (mu4e-message-field-at-point :references) ref)
		 ref (mu4e-message-field-at-point :references))
	(when (-contains? (mu4e-message-field-at-point :references) ref)
	  (message "confirmed")
	  (eval action))))

(add-hook 'mu4e-view-mode-hook 'check-for-followup)
#+END_SRC

#+RESULTS:
| check-for-followup | gilgamesh-fontify | mu4e-auto-tag | email-fontify | #[nil \301\300!\210\302\211\207 [bookmark-make-record-function make-local-variable mu4e-view-bookmark-make-record] 2] |

** Tagging emails

** Automatic tagging

* file based interaction with mongoDb


This did not work because the shell command was too long to insert 6047 contacts, and doing them one at a time took over ten minutes.

#+BEGIN_SRC emacs-lisp
(mongo-insert "contacts" "contacts"
	      (loop for contact in contacts
		    collect
		    (append `((desc . ,(car contact))) (cdr contact))))
#+END_SRC

Here I try writing a js file and calling it.

#+BEGIN_SRC emacs-lisp
(with-temp-file "write.js"
  (let* ((data (loop for contact in contacts
		     collect 
		     (append `((desc . ,(car contact))) (cdr contact))))
	 (json (json-encode data))
	 (json (replace-regexp-in-string "[^[:ascii:]]" "-" json))) 
    (insert "db.contacts.insert(" json ")")))
#+END_SRC

#+RESULTS:

#+BEGIN_SRC emacs-lisp
(measure-time
 (shell-command "mongo 127.0.0.1/contacts --quiet --eval 'load(\"write.js\")'"))
#+END_SRC

#+RESULTS:
: 0.576353 seconds elapsed

This, is blazing fast once the file is created. This could be a good strategy for large writes or large sets of commands.
* A comint mode for mongo
Adapted from: https://www.masteringemacs.org/article/comint-writing-command-interpreter

** using commands with comint

#+BEGIN_SRC emacs-lisp
(setq mongo-output '())
#+END_SRC

#+RESULTS:

#+BEGIN_SRC emacs-lisp
(mongo-cmd "use bibtex")
#+END_SRC

#+RESULTS:
: switched to db bibtex

#+BEGIN_SRC emacs-lisp
(mongo-cmd "show dbs")
#+END_SRC

#+RESULTS:
: admin     0.000GB
: contacts  0.002GB
: local     0.000GB
: test      0.005GB



#+BEGIN_SRC emacs-lisp
(mongo-cmd "db.contacts.count()")
#+END_SRC

#+RESULTS:
: 0

#+BEGIN_SRC emacs-lisp
(mongo-use "contacts")
(mongo-count "entries")
#+END_SRC

#+RESULTS:
: 3

#+BEGIN_SRC emacs-lisp
(mongo-insert "entries" (loop for contact in contacts
			      collect 
			      (append `(,(cons 'desc (car contact))) (cdr contact))))
#+END_SRC

#+RESULTS:
: 2017

#+BEGIN_SRC emacs-lisp
(mongo-count "entries")
#+END_SRC

#+RESULTS:
: 3

#+BEGIN_SRC emacs-lisp
(mongo-count "contacts" nil '((limit . 5)))
#+END_SRC

#+RESULTS:
: 0

#+BEGIN_SRC emacs-lisp
(mongo-insert "entries" '((name . "John Kitchin")))
#+END_SRC

#+RESULTS:
: ((nInserted . 1))



#+BEGIN_SRC emacs-lisp :results code
(mongo-find "contacts" '((EMAIL . (($regex . "cmu.edu")))))
#+END_SRC

#+RESULTS:
#+BEGIN_SRC emacs-lisp
[]
#+END_SRC




#+BEGIN_SRC emacs-lisp
(mongo-update "contacts" '((name . "John Kitchin")) '((email . "test")))
#+END_SRC

#+RESULTS:
: ((nMatched . 1) (nUpserted . 0) (nModified . 1))

#+BEGIN_SRC emacs-lisp
(mongo-update "contacts" '((name . "John Kitchin")) '(($set . ((email . "test")))))
#+END_SRC

#+RESULTS:
: ((nMatched . 1) (nUpserted . 0) (nModified . 1))

#+BEGIN_SRC emacs-lisp
(mongo-deleteMany "contacts" '((name . "John Kitchin")))
#+END_SRC

#+RESULTS:
: ((acknowledged . t) (deletedCount . 0))

#+BEGIN_SRC emacs-lisp
(mongo-count "contacts")
#+END_SRC

#+RESULTS:
: 6

#+BEGIN_SRC emacs-lisp
(mongo-drop "contacts")
#+END_SRC

#+RESULTS:
: true
#+BEGIN_SRC emacs-lisp
(mongo-dropDatabase)
#+END_SRC

#+RESULTS:
: {  "dropped" : "bibtex",  "ok" : 1 }

#+BEGIN_SRC emacs-lisp
(mongo-use "contacts")
#+END_SRC

#+RESULTS:
: switched to db contacts
* DONE ob-ipython and inline figures in org-mode
  CLOSED: [2017-01-29 Sun 16:05]
  :PROPERTIES:
  :categories: python,ipython
  :date:     2017/01/29 16:05:22
  :updated:  2017/01/29 16:05:22
  :END:

[[https://github.com/gregsexton/ob-ipython][ob-ipython]] provides some nice support for inline images, but it is a little limited. You can only have one inline plot, and you cannot capture the printed output. I often want both, and use more than one figure in a code block. So, here I look at a way to get that. 

When ob-ipython executes a cell, it gets two things internally: the output and a list of result elements. The output is all the stuff that is printed, and the result contains result cells. So, we just have to check these for images, and append them to the output in an appropriate way.  I will do that using file links so that org automatically renders them. We will save the images as temp files, since they are regenerated each time you run the cell. 

I want output and inline figures. This ipython block should output some text and two figures. Note we do not define file names anywhere! See [[id:44FC1FFF-A6EA-466E-B61C-85B22E58781D][this section]] for details on how to get ob-ipython to do this.

#+BEGIN_SRC ipython :session :results output drawer
%matplotlib inline
import matplotlib.pyplot as plt
import numpy as np

t = np.linspace(0, 20 * np.pi, 350)
x = np.exp(-0.1 * t) * np.sin(t)
y = np.exp(-0.1 * t) * np.cos(t)

plt.plot(x, y)
plt.axis('equal')

plt.figure()
plt.plot(y, x)
plt.axis('equal')

print('Length of t = {}'.format(len(t)))
print('x .dot. y = {}'.format(x @ y))
#+END_SRC

#+RESULTS:
:RESULTS:
Length of t = 350
x .dot. y = 1.3598389888491538
[[file:/var/folders/5q/lllv2yf95hg_n6h6kjttbmdw0000gn/T/ob-ipython-86557tr2.png]]
[[file:/var/folders/5q/lllv2yf95hg_n6h6kjttbmdw0000gn/T/ob-ipython-86557f1F.png]]
:END:

Nice, success! Now my code blocks [[http://kitchingroup.cheme.cmu.edu/blog/2017/01/21/Exporting-org-mode-to-Jupyter-notebooks/][export more cleanly to jupyter notebooks]]. Speaking of which, if you liked the post on that, there is a new library for it in scimax: https://github.com/jkitchin/scimax/blob/master/ox-ipynb.el. Yes, one day I will put it in its own repo, and probably put it up on MELPA. If it turns out to be useful over the next semester. 



** code for getting output and inline figures
   :PROPERTIES:
   :ID:       44FC1FFF-A6EA-466E-B61C-85B22E58781D
   :END:

I wrote one new function that writes the base64 data out to a temporary file and returns a link to it. Then, I modified the org-babel-execute:ipython function to append these links onto the output. It seems like you need to use a header like this in your ob-ipython block, notably the results need to be in a drawer like this if you want org-mode to render the images. They do not show up in the results that have colons starting them.

#+BEGIN_EXAMPLE
#+BEGIN_SRC ipython :session :results output drawer
#+END_EXAMPLE

Here is the code.

#+BEGIN_SRC emacs-lisp
(defun ob-ipython-inline-image (b64-string)
  "Write the b64-string to a temporary file.
Returns an org-link to the file."
  (let* ((tfile (make-temp-file "ob-ipython-" nil ".png"))
	 (link (format "[[file:%s]]" tfile)))
    (ob-ipython--write-base64-string tfile b64-string)
    link))


(defun org-babel-execute:ipython (body params)
  "Execute a block of IPython code with Babel.
This function is called by `org-babel-execute-src-block'."
  (let* ((file (cdr (assoc :file params)))
         (session (cdr (assoc :session params)))
         (result-type (cdr (assoc :result-type params))))
    (org-babel-ipython-initiate-session session params)
    (-when-let (ret (ob-ipython--eval
                     (ob-ipython--execute-request
                      (org-babel-expand-body:generic (encode-coding-string body 'utf-8)
                                                     params (org-babel-variable-assignments:python params))
                      (ob-ipython--normalize-session session))))
      (let ((result (cdr (assoc :result ret)))
            (output (cdr (assoc :output ret))))
        (if (eq result-type 'output)
	    (concat
	     output 
	     (format "%s"
		     (mapconcat 'identity
				(loop for res in result
				      if (eq 'image/png (car res))
				      collect (ob-ipython-inline-image (cdr res)))
				"\n")))
          (ob-ipython--create-stdout-buffer output)
          (cond ((and file (string= (f-ext file) "png"))
                 (->> result (assoc 'image/png) cdr (ob-ipython--write-base64-string file)))
                ((and file (string= (f-ext file) "svg"))
                 (->> result (assoc 'image/svg+xml) cdr (ob-ipython--write-string-to-file file)))
                (file (error "%s is currently an unsupported file extension." (f-ext file)))
                (t (->> result (assoc 'text/plain) cdr))))))))
#+END_SRC

#+RESULTS:
: org-babel-execute:ipython

